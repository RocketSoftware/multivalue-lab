*$options DEFAULT
subroutine CalculateMean(Result,Times,Iterations,MeanIterations)
******************************************************************************
*
*     Calculate Mean average
*
*     Module %M%    Version  %I%   Date  %H%
*
*	IBM Confidential
*	OCO Source Materials
*	Copyright (C) IBM Corp.  2005
*
*     (c) Copyright 2005 IBM Corp. - All Rights Reserved
*     This is unpublished proprietary source code of IBM Corp.
*     The copyright notice above does not evidence any actual or intended
*     publication of such source code.
*
*******************************************************************************
*
*     Maintenence log - insert most recent change descriptions at top
*
*     Date.... ECase WHO Description........................................
*     02/02/05       MAB Created Module
*
*******************************************************************************

* General routine to take a multivalued list of numerator values (Times)
* and denominator values (Iterations) to produce a total average 
* sum(Times) / sum(Iterations)
* The two dynamic arrays must be separated by corresponding matching mark
* characters.
* Individual instances of the denominator value may be zero, as long as the
*   sum of the denominator values is not zero.

* The Result of "" is returned if:
*   . There are no numerator values.
*   . The mark characters do not match in the two arrays
*   . By extension, the arrays must correspond in value length
*   . The sum(Iterations) must not be zero.

* Times:      Dynamic array of sample times (numerator value)
* Iterations: Dynamic array of loop count (denominator value)

* If there are no Times, return default Result
if len(Times) = 0 then
	Result = ""
	MeanIterations = ""
	return
end
TotalTime       = 0
TotalIterations = 0
SampleCount     = 0
loop
	Numerator   = remove(Times,DelimiterTimes)
	Denominator = remove(Iterations,DelimiterIterations)
	if len(Denominator) > 0 and Denominator # 0 then
		SampleCount     += 1
		TotalTime       += Numerator
		TotalIterations += Denominator
	end
	if DelimiterTimes # DelimiterIterations then
		Result = ""
		MeanIterations = ""
		return
	end
while DelimiterTimes # 0
repeat
if TotalIterations = 0 then
	Result = ""
	MeanIterations = ""
	return
end
Result = TotalTime / TotalIterations
MeanIterations = TotalIterations / SampleCount
return
